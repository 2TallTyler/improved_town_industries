
/* *** Begin Paper Mill *** */

spritelayout sprlay_paper_mill_9_N {
	ground		{sprite:3981;}
	building	{sprite:2169; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}
spritelayout sprlay_paper_mill_9_E {
	ground		{sprite:3981;}
	building	{sprite:2170; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}
spritelayout sprlay_paper_mill_9_W {
	ground		{sprite:3981;}
	building	{sprite:2171; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}
spritelayout sprlay_paper_mill_9_S {
	ground		{sprite:3981;}
	building	{sprite:2172; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}

spritelayout sprlay_paper_mill_chem_1 { // Tall tank
	ground		{sprite:1420;}
	building	{sprite:2083; yoffset:4; xoffset:4; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}
spritelayout sprlay_paper_mill_chem_2 { // Pipe frame 1
	ground		{sprite:1420;}
	building	{sprite:2089; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}
spritelayout sprlay_paper_mill_chem_3 { // Pipe frame 2
	ground		{sprite:1420;}
	building	{sprite:2092; recolour_mode: RECOLOUR_REMAP; palette: PALETTE_USE_DEFAULT;}
}
spritelayout sprlay_paper_mill_logs_1 {
	ground		{sprite:2022;}
	building	{sprite:2071;}
}

spritelayout sprlay_paper_mill_logs_2 {
	ground		{sprite:2022;}
	building	{sprite:2070;}
}

spritelayout sprlay_paper_mill_transformers {
	ground		{sprite:2022;}
	building	{sprite:2054;}
}

spritelayout sprlay_paper_mill_empty {
	ground		{sprite:2022;}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_9_N) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_9_N;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_9_E) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_9_E;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_9_W) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_9_W;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_9_S) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_9_S;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_chem_1) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_chem_1;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_chem_2) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_chem_2;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_chem_3) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_chem_3;}
}
item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_logs_1) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_logs_1;}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_logs_2) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_logs_2;}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_transformers) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_transformers;}
}

item (FEAT_INDUSTRYTILES, ind_tile_paper_mill_empty) {
	property {
		substitute:				02;
		special_flags:			bitmask(INDTILE_FLAG_ACCEPT_ALL);
		accepted_cargos: 		[[PASS, 8]];
	}
	graphics {default:			sprlay_paper_mill_empty;}
}

tilelayout industry_layout_paper_mill_1 {
0,0:	ind_tile_paper_mill_9_N;	0,1:	ind_tile_paper_mill_9_E;	0,2:	ind_tile_paper_mill_chem_2;	0,3:	ind_tile_paper_mill_chem_1;
1,0:	ind_tile_paper_mill_9_W;	1,1:	ind_tile_paper_mill_9_S;	1,2:	ind_tile_paper_mill_9_N;	1,3:	ind_tile_paper_mill_9_E;
2,0:	ind_tile_paper_mill_chem_1;	2,1:	ind_tile_paper_mill_chem_3;	2,2:	ind_tile_paper_mill_9_W;	2,3:	ind_tile_paper_mill_9_S;
3,0:	ind_tile_paper_mill_9_N;	3,1:	ind_tile_paper_mill_9_E;	3,2:	ind_tile_paper_mill_logs_1;	3,3:	ind_tile_paper_mill_logs_1;
4,0:	ind_tile_paper_mill_9_W;	4,1:	ind_tile_paper_mill_9_S;	4,2:	ind_tile_paper_mill_logs_1;	4,3:	ind_tile_paper_mill_logs_1;
}

tilelayout industry_layout_paper_mill_2 {
0,0:	ind_tile_paper_mill_9_N;			0,1:	ind_tile_paper_mill_9_E;	0,2:	ind_tile_paper_mill_logs_1;
1,0:	ind_tile_paper_mill_9_W;			1,1:	ind_tile_paper_mill_9_S;	1,2:	ind_tile_paper_mill_logs_1;		1,3:	ind_tile_paper_mill_empty;
2,0:	ind_tile_paper_mill_transformers;	2,1:	ind_tile_paper_mill_9_N;	2,2:	ind_tile_paper_mill_9_E;		2,3:	ind_tile_paper_mill_logs_2;
											3,1:	ind_tile_paper_mill_9_W;	3,2:	ind_tile_paper_mill_9_S;
}

tilelayout industry_layout_paper_mill_3 {
0,0:	ind_tile_paper_mill_chem_3;		0,1:	ind_tile_paper_mill_chem_1;		0,2:	ind_tile_paper_mill_9_N;	0,3:	ind_tile_paper_mill_9_E;
1,0:	ind_tile_paper_mill_logs_1;		1,1:	ind_tile_paper_mill_empty;		1,2:	ind_tile_paper_mill_9_W;	1,3:	ind_tile_paper_mill_9_S;
2,0:	ind_tile_paper_mill_logs_2;		2,1:	ind_tile_paper_mill_9_N;		2,2:	ind_tile_paper_mill_9_E;
3,0:	ind_tile_paper_mill_logs_1;		3,1:	ind_tile_paper_mill_9_W;		3,2:	ind_tile_paper_mill_9_S;
}

/* Industry Construction Switches - Read from bottom to top */

/* Don't allow the same industry within 100 tiles - secondary industries only */
switch (FEAT_INDUSTRIES, SELF, switch_paper_mill_distance_competitor, industry_distance(industry_paper_mill, -1) > 100 ) {1: CB_RESULT_LOCATION_ALLOW; CB_RESULT_LOCATION_DISALLOW;}

/* Population Check - secondary industries only */
switch (FEAT_INDUSTRIES, PARENT, switch_paper_mill_population_check_1, // Check if there's enough population in the town to support the new industry in additon to whatever industries already exist (300 population per industry)
	[switch_secondary_population_store(), population >= LOAD_TEMP(1)]) 
		{1: switch_paper_mill_distance_competitor; CB_RESULT_LOCATION_DISALLOW;}

/* Town Distance Check - secondary industries only */

/* Ensure processing industries are near towns. Distance measurement is to town sign, so this adds the town zone radii to scale the distance requirement to the town size */
switch (FEAT_INDUSTRIES, SELF, switch_paper_mill_town_distance_check_2, town_euclidean_dist(0,0) <= LOAD_TEMP(0)+225) {1: switch_paper_mill_population_check_1; return CB_RESULT_LOCATION_DISALLOW;} 
	// Add 15 to the town zone radii and check if the distance to the town sign is smaller than that radius. The town zone radii and distance check use the square of the distance, thus 225 = 15 tiles. 
switch (FEAT_INDUSTRIES, PARENT, switch_paper_mill_town_distance_check_1, STORE_TEMP(town_zone_0_radius_square + town_zone_1_radius_square + town_zone_2_radius_square + town_zone_3_radius_square + town_zone_4_radius_square, 0)) {switch_paper_mill_town_distance_check_2;} 
	// Add up the town zone radii and store it in temporary register 0


/* If "Generate primary only" parameter enabled, disallow random generation of secondary industries */
switch (FEAT_INDUSTRIES, SELF, switch_paper_mill_generate_primary_only, param_primary_only)
	{1: return CB_RESULT_LOCATION_DISALLOW; switch_paper_mill_town_distance_check_1;}

/* Prospected industries aren't restricted by industry totals, but must meet other requirements */
switch (FEAT_INDUSTRIES, SELF, switch_paper_mill_allow_prospected, extra_callback_info2 == IND_CREATION_PROSPECT || game_mode == GAMEMODE_EDITOR)
	{1: switch_paper_mill_town_distance_check_1; switch_paper_mill_generate_primary_only;}

/* Funded industries have no restrictions */
switch (FEAT_INDUSTRIES, SELF, switch_paper_mill_allow_funded, extra_callback_info2 == IND_CREATION_FUND) 
	{1: return CB_RESULT_LOCATION_ALLOW; switch_paper_mill_allow_prospected;}

item (FEAT_INDUSTRIES, industry_paper_mill, 7) {
	property {
		substitute:				INDUSTRYTYPE_PAPER_MILL;
		life_type:				IND_LIFE_TYPE_PROCESSING;
		spec_flags:				(param_primary_only == 1) ? bitmask(IND_FLAG_DO_NOT_FORCE_INSTANCE_AT_MAP_GENERATION) : 0;
		cargo_types:			[
								accept_cargo("WOOD", produce_cargo("GOOD",1), produce_cargo("PASS",0.25)),
								accept_cargo("RFPR", produce_cargo("GOOD",1), produce_cargo("PASS",0.25)),
								accept_cargo("RCYC", produce_cargo("GOOD",1), produce_cargo("PASS",0.25)),
		];
		name:					TTD_STR_INDUSTRY_NAME_PAPER_MILL;
		nearby_station_name:	string(STR_STATION, string(STR_TOWN), string(STR_NAME_PAPER_MILL));
		prob_map_gen:			1;
		prob_in_game:			1;
		fund_cost_multiplier:	200;
		map_colour:				184;
		layouts:				[
			industry_layout_paper_mill_1,
			industry_layout_paper_mill_2,
			industry_layout_paper_mill_3,
		];
		conflicting_ind_types:	[industry_forest, industry_farm];
	}
	graphics {
		location_check:			switch_paper_mill_allow_funded;
		construction_probability:	(current_year >= 1885) ? 3 : 0;
		extra_text_fund:		string(STR_FUND_AVAILABLE_1885);
		colour:					COLOUR_DARK_GREEN;
	}
}

switch (FEAT_OBJECTS, SELF, switch_object_graphics_paper_mill_9, relative_pos) {
	relative_coord(0, 0): sprlay_paper_mill_9_N;
	relative_coord(0, 1): sprlay_paper_mill_9_E;
	relative_coord(1, 0): sprlay_paper_mill_9_W;
    relative_coord(1, 1): sprlay_paper_mill_9_S;
}

item (FEAT_OBJECTS, object_item_paper_mill_9) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					TTD_STR_INDUSTRY_NAME_PAPER_MILL;
		climates_available:		ALL_CLIMATES;
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[2,2];
		introduction_date:		1845;
		num_views:				1;
	}
	graphics {
		default:				switch_object_graphics_paper_mill_9;
		colour:					COLOUR_DARK_GREEN;
	}
}

/* *** End Paper Mill *** */
